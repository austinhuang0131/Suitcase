/*
 * Copyright 2016-2019 Julien Guerinet
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply from: "https://raw.githubusercontent.com/jguerinet/Gradle-Artifact-Scripts/master/spotless.gradle"
apply plugin: 'com.github.ben-manes.versions'

buildscript {

    // Versions shared across modules
    ext.appcompat_version = '1.0.2'
    ext.espresso_version = '3.0.2'
    ext.junit_version = '4.12'
    ext.kotlin_version = '1.3.21'
    ext.threetenabp_version = '1.1.2'
    ext.lifecycle_version = "2.0.0"

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.1'
        classpath 'com.diffplug.spotless:spotless-plugin-gradle:3.18.0'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.21.0'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
        classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:0.9.17"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects {
    apply plugin: 'com.android.library'
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-kapt'
    apply plugin: 'org.jetbrains.dokka-android'
    apply plugin: 'com.github.dcendents.android-maven'

    android {
        compileSdkVersion Integer.valueOf(suitcase_target_sdk)

        defaultConfig {
            minSdkVersion suitcase_min_sdk
            targetSdkVersion suitcase_target_sdk
            versionName suitcase_version

            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        }

        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            }
        }
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
        androidTestImplementation('androidx.test.espresso:espresso-core:3.1.1', {
            exclude group: 'com.android.support', module: 'support-annotations'
        })
        testImplementation "junit:junit:$junit_version"
    }

    apply from: "https://raw.githubusercontent.com/jguerinet/Gradle-Artifact-Scripts/master/android-kotlin-artifacts.gradle"
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'b', 'ea'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]$qualifier[.\d-+]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}
